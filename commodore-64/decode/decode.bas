100 REM: DECODE
110 REM: COPYRIGHT 1983
111 REM: TOM RUGG, PHIL FELDMAN, AND
112 REM: WESTERN SYSTEMS GROUP
120 D=6:P=4:L=12
130 DIM G$(L),G(P),C(P),B(L),W(L)
140 R=RND(-TI)
150 GOSUB 1200
170 GOSUB 300:GOSUB 370
180 PRINT"GUESS NUMBER";G;
190 INPUT A$
200 IF LEFT$(A$,1)="S" THEN 500
210 IF LEFT$(A$,1)="Q" THEN 600
220 GOSUB 700
230 GOSUB 800
240 GOSUB 1000
250 IF B(G)=P THEN 2000
260 G$(G)=A$
270 G=G+1:IF G>L THEN 2200
280 GOTO 180
300 G=1:C$=""
310 RETURN
370 FOR J=1 TO P
380 R=INT(D*RND(1))+1
390 C$=C$+MID$(STR$(R),2,1)
400 NEXT J
410 PRINT "I'VE CHOSEN MY SECRET CODE."
420 PRINT
430 RETURN
500 IF G=1 THEN PRINT "NO GUESSES YET":GOTO 180
510 PRINT CHR$(147),"SUMMARY":PRINT
520 PRINT"NO.    GUESS   BLACK   WHITE"
530 PRINT:FOR J=1 TO G-1
540 PRINT J;TAB(7);G$(J);TAB(16);B(J);TAB(24);W(J)
550 IF G<10 THEN PRINT
560 NEXT:PRINT
570 GOTO 180
600 PRINT
610 PRINT"CAN'T TAKE IT, HUH?"
620 PRINT:PRINT"WELL, MY CODE WAS ";
630 FOR J=1 TO 4
640 PRINT" .";
650 FOR K=1 TO 900:NEXT
660 NEXT
670 PRINT C$:PRINT
680 GOTO 2090
700 IF LEN(A$)<>P THEN 780
710 FOR J=1 TO P
720 R=VAL(MID$(A$,J,1))
730 IF R<1 OR R>D THEN 780
740 NEXT
750 RETURN
780 PRINT"ILLEGAL.  TRY AGAIN."
790 GOTO 180
800 B=0:W=0
810 FOR J=1 TO P
820 G(J)=VAL(MID$(A$,J,1))
830 C(J)=VAL(MID$(C$,J,1))
840 IF G(J)=C(J) THEN B=B+1:G(J)=0:C(J)=0
850 NEXT
860 FOR J=1 TO P:IF C(J)=0 THEN 920
870 H=0:FOR K=1 TO P
880 IF C(J)=0 THEN 910
890 IF C(J)<>G(K) THEN 910
900 H=1:G(K)=0:C(J)=0
910 NEXT K:W=W+H
920 NEXT J
930 RETURN
1000 B(G)=B:W(G)=W:PRINT
1010 PRINT"GUESS NO.";G;"-- BLACK =";B;
1015 PRINT" WHITE =";W
1020 PRINT:RETURN
1200 PRINT CHR$(147);
1210 PRINT"**** DECODE ****"
1220 PRINT:PRINT
1230 PRINT"FIGURE OUT A";P;"POSITION CODE"
1240 PRINT
1250 PRINT "USING THE DIGITS 1 THRU";D
1260 PRINT:PRINT
1270 PRINT"'BLACK' INDICATES A CORRECT DIGIT"
1280 PRINT:PRINT"IN THE RIGHT POSITION."
1290 PRINT
1300 PRINT"'WHITE' INDICATES SOME OTHER CORRECT"
1310 PRINT
1320 PRINT "DIGIT, BUT IN THE WRONG POSITION."
1330 PRINT:PRINT
1340 RETURN
2000 PRINT
2010 PRINT "YOU GOT IT IN";G;"GUESSES."
2020 IF G<5 THEN B$="OUTSTANDING!"
2030 IF G=5 OR G=6 THEN B$="PRETTY GOOD"
2040 IF G=7 THEN B$="NOT BAD"
2050 IF G=8 THEN B$="NOT TOO GREAT"
2060 IF G>8 THEN B$="PRETTY BAD"
2070 PRINT:PRINT"...THAT'S ";B$
2080 PRINT
2090 INPUT"WANT TO TRY AGAIN";A$
2100 IF LEFT$(A$,1)="Y" THEN 150
2110 IF LEFT$(A$,1)<>"N" THEN 2090
2120 PRINT:PRINT "COWARD.":PRINT
2130 END
2200 PRINT
2210 PRINT"THAT'S YOUR LIMIT OF";L;"GUESSES."
2220 PRINT
2230 PRINT"MY CODE WAS ";C$
2240 GOTO 2080