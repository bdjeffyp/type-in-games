100 REM: GROAN
110 REM: COPYRIGHT 1983
111 REM: PHIL FELDMAN, TOM RUGG, AND
112 REM: WESTERN SYSTEMS GROUP
150 Q=RND(-TI):
160 S=1024: OS=54272
170 DL=200
180 B=166:D=168:U=196
190 POKE 53280,6:POKE 53281,6
195 PRINT CHR$(5)
200 PRINT CHR$(147);TAB(16);"GROAN":PRINT
210 PRINT:PRINT "HOW MUCH NEEDED TO WIN"
215 INPUT"(BETWEEN 50-100 IS BEST) ";W
220 W=INT(W):IF W<=0 THEN 210
230 PRINT:PRINT
232 PRINT "AND NOW A COIN TOSS FOR FIRST ROLL"
234 GOSUB 830
240 PRINT:PRINT:PRINT "THE COIN IS IN THE AIR AND"
250 Q$="YOU":Q=RND(1):IF Q>.5 THEN Q$="I"
260 PRINT:PRINT SPC(5);
265 FOR J=1 TO 5:PRINT". ";:GOSUB 830:NEXT
270 PRINT Q$;" GET FIRST ROLL":GOSUB 840
275 POKE 53280,5:POKE 53281,5:PRINT CHR$(5)
280 T=0:IF Q>.5 THEN 400
300 P$=" YOU"
305 PRINT CHR$(147);TAB(11);"YOU'RE ROLLING"
308 GOSUB 830:GOSUB 500
310 T=T+R1+R2:IF F>0 THEN T=0
320 IF F=2 THEN H=0
330 GOSUB 850
333 IF F>0 THEN PRINT"DICE PASS TO ME"
336 IF F>0 THEN GOSUB 840:GOTO 400
340 PRINT"(P=PASS DICE - R=ROLL AGAIN)"
345 PRINT:PRINT"YOUR DECISION (P OR R) ?"
350 GET Q$:IF Q$="" THEN 350
360 IF Q$="R" THEN 300
370 IF Q$<>"P" THEN 350
380 PRINT:H=H+T:IF H>=W THEN 970
390 T=0:F=1:PRINT CHR$(147):GOTO 330
400 T=0:P$="I"
410 PRINT CHR$(147);CHR$(17);TAB(12);"I'M ROLLING"
415 GOSUB 830:GOSUB 500
420 T=T+R1+R2:IF F>0 THEN T=0
430 IF F=2 THEN P=0
440 GOSUB 850:IF F>0 THEN PRINT"DICE PASS TO YOU"
445 IF F>0 THEN GOSUB 840:T=0:GOTO 300
450 GOSUB 1000:IF X=1 THEN PRINT"I'LL ROLL AGAIN"
455 IF X=1 THEN GOSUB 840:GOTO 410
460 PRINT"I'LL STOP WITH THIS"
465 GOSUB 830:P=P+T:IF P>=W THEN 970
470 PRINT:PRINT"DICE PASS TO YOU"
475 T=0:GOSUB 840:GOTO 300
500 C2=S+420:DL=500:GOSUB 600:R1=INT(RND(1)*6+1)
505 R2=INT(RND(1)*6+1)
510 DL=60:C2=C2-118:GOSUB 600:C2=C2-77:GOSUB 600
515 C2=C2+83:GOSUB 600
520 C2=C2+122:GOSUB 600:C2=C2+118:GOSUB 600
525 C2=C2+77:GOSUB 600
530 C2=C2-83:GOSUB 600
540 C2=C2-122:C1=2*(S+417)-C2
550 C=C1:R=R1:GOSUB 650:GOSUB 700
560 C=C2:R=R2:GOSUB 650:GOSUB 700:F=0
565 IF R1=1 THEN F=1:NT=5:GOSUB 800
570 IF R2=1 THEN F=F+1:NT=25:GOSUB 800
580 IF F=2 THEN GOSUB 820:GOSUB 830
590 RETURN
600 C1=2*(S+417)-C2:C=C1:GOSUB 650
610 C=C2:GOSUB 650:FOR J=1 TO DL:NEXT
620 PRINT CHR$(147):RETURN
650 POKE C-82,85:POKE C-82+OS,1:POKE C-78,73
655 POKE C-78+OS,1:POKE C+78,74:POKE C+78+OS,1
660 POKE C+82,75:POKE C+82+OS,1:FOR J=1 TO 3
665 POKE C-82+J,64:POKE C-82+J+OS,1:POKE C-78+40*J,93
670 POKE C-78+40*J+OS,1:POKE C-82+40*J,93
675 POKE C-82+40*J+OS,1:POKE C+78+J,64
680 POKE C+78+J+OS,1:NEXT:RETURN
700 ON R GOSUB 705,725,735,740,750,755:RETURN
705 POKE C-41,87:POKE C-41+OS,1:POKE C-39,87
710 POKE C-39+OS,1:POKE C,46:POKE C+OS,1
715 POKE C+39,85:POKE C+39+OS,1:POKE C+40,64
720 POKE C+40+OS,1:POKE C+41,73:POKE C+41+OS,1:RETURN
725 POKE C-41,81:POKE C-41+OS,1:POKE C+41,81
730 POKE C+41+OS,1:RETURN
735 POKE C,81:POKE C+OS,1:GOSUB 725:RETURN
740 GOSUB 725:POKE C-39,81:POKE C-39+OS,1
745 POKE C+39,81:POKE C+39+OS,1:RETURN
750 GOSUB 740:POKE C,81: POKE C+OS,1:RETURN
755 GOSUB 740: POKE C-1,81:POKE C-1+OS,1
760 POKE C+1,81:POKE C+1+OS,1:RETURN
800 PRINT CHR$(19)
805 FOR J=1 TO 9:PRINT CHR$(17);:NEXT
810 PRINT TAB(NT);"GROAN":GOSUB 830:RETURN
820 FOR J=1 TO 5:PRINT CHR$(145);:NEXT
825 PRINT TAB(14);CHR$(18);"DESPAIR":RETURN
830 FOR K=L TO 1500:NEXT:RETURN
840 FOR K=L TO 5000:NEXT:RETURN
850 PRINT CHR$(19)
853 FOR J=1 TO 13:PRINT CHR$(17);:NEXT
856 PRINT:FOR J=1 TO 11
860 PRINT CHR$(B);:NEXT
863 PRINT" SCOREBOARD ";
866 FOR J=1 TO 12:PRINT CHR$(B);:NEXT
868 PRINT
870 GOSUB 960:PRINT CHR$(B);SPC(3);W;
875 PRINT "POINTS NEEDED TO WIN";TAB(34);CHR$(B)
880 PRINT CHR$(B);
883 FOR J=1 TO 33:PRINT CHR$(D);:NEXT
886 PRINT CHR$(B):GOSUB 960
890 PRINT CHR$(B);" POINTS SCORED     YOU     ME";
895 PRINT TAB(34);CHR$(B)
900 PRINT CHR$(B);"  BEFORE THIS";
905 PRINT TAB(20);CHR$(U);CHR$(U);CHR$(U);
910 PRINT TAB(28);CHR$(U);CHR$(U);TAB(34);CHR$(B)
920 PRINT CHR$(B);"    SERIES";TAB(19);H;
925 PRINT TAB(27);P;TAB(34);CHR$(B)
930 PRINT CHR$(B);
933 FOR J=1 TO 33:PRINT CHR$(D);:NEXT
936 PRINT CHR$(B):GOSUB 960
940 PRINT CHR$(B);P$;
943 PRINT " HAVE";T;"POINTS THIS SERIES";
946 PRINT TAB(34);CHR$(B):GOSUB 960
950 FOR J=1 TO 35:PRINT CHR$(B);:NEXT
955 PRINT:PRINT:RETURN
960 PRINT CHR$(B);TAB(34);CHR$(B):RETURN
970 T=0:PRINT CHR$(147):GOSUB 850
975 IF P>=W THEN PRINT:PRINT"SKILL WINS AGAIN"
980 IF H>=W THEN PRINT"YOU WIN -";
985 IF H>=W THEN PRINT" IT WAS SHEER LUCK"
990 POKE 53280,14:POKE 53281,6
995 END
1000 V=P+T:IF V>=W THEN 1100
1010 IF (W-H)<10 THEN 1110
1020 IF P>=H THEN L=T/25:GOTO 1050
1030 IF V<H THEN L=T/35:GOTO 1050
1040 L=T/30
1050 IF RND(1)>L THEN 1110
1100 X=0:RETURN
1110 X=L:RETURN